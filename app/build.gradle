apply plugin: 'com.android.application'

android {
    compileSdkVersion 31
    buildToolsVersion "31.0.0"
    ndkVersion "23.1.7779620"

    defaultConfig {
        applicationId "com.emanuelef.remote_capture"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 37
        versionName "1.4.3"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            if(project.hasProperty("doNotStrip")) {
                // NOTE: packagingOptions will also affect the "release" build, so the "doNotStrip"
                // guard is required
                packagingOptions {
                    // NOTE: unstripped nDPI library takes up about 4 MB!
                    //doNotStrip '**.so'
                    doNotStrip '**/libpcapd.so'
                    doNotStrip '**/libvpnproxy-jni.so'
                }
            }
        }
    }

    externalNativeBuild {
        cmake {
            path file('src/main/jni/CMakeLists.txt')
            version "3.18.1"
        }
    }

    compileOptions {
        encoding "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests.includeAndroidResources = true
    }

}

dependencies {
// AndroidX
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'
    implementation 'androidx.fragment:fragment:1.3.6'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.0.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'

// Google
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'com.google.code.gson:gson:2.8.7'

// Third-party
    implementation 'cat.ereza:customactivityoncrash:2.3.0'
    implementation 'org.nanohttpd:nanohttpd:2.3.1'

// Test
    testImplementation 'junit:junit:4.13.2'

// Robolectric environment
    testImplementation 'androidx.test:core:1.4.0'
    testImplementation "org.robolectric:robolectric:4.5.1"

// Mockito framework
    testImplementation 'org.mockito:mockito-core:1.10.19'

// Truth assert
    testImplementation "com.google.truth:truth:1.1.3"


// AndroidJUnitRunner and JUnit Rules
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'


}